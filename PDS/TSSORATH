        TITLE  'T S S O   R A C F   AUTHORIZATION CHECKING PROCESSING'
TSSORATH CSECT
***********************************************************************
* THIS MODULE WILL CALL RACF TO CHECK IF THE USER IS AUTHORIZED TO    *
* EXECUTE THE COMMAND.  THIS MODULES PROCESSING IS TO BE USED TO      *
* REPLACE THE CHECKING OF THE UADS USERDATA FIELD FOR COMMAND         *
* AUTHORIZATION.                                                      *
*                                                                     *
* PARAMETERS PASSED:                                                  *
*   NONE                                                              *
*                                                                     *
*                                                                     *
*    RETURN CODES:                                                    *
*                                                                     *
*    R15 = 0 : USER AUTHORIZED                                        *
*    R15 = 4 : FACILITY CLASS PROFILE NOT FOUND                       *
*    R15 = 8 : USER NOT AUTHORIZED                                    *
*                                                                     *
* THE MODULE WILL ISSUE ITS OWN MESSAGES FOR FAILURE REASONS....      *
*                                                                     *
*                                                                     *
* MODULE HISTORY:                                                     *
*    DEC 2002 PVW:  INITIALLY CREATED                                 *
*                                                                     *
* NOTES:                                                              *
*                                                                     *
*    THIS MODULE SHOULD WORK FOR ANY SECURITY SYSTEM THAT CLAIMS TO   *
*    SUPPORT THE SAF INTERFACE.                                       *
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF TSSO
         USING TSSORATH,R12       GET ADDRESSIBILITY
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=2048         GET STORAGE FOR A LOCAL
         LR    R11,R1             DATA AREA
         USING TSSORDAT,R11       AND TELL ASSEMBLER
*
         MVC   RACMACRO(RACLEN),RACTEMP
         LA    R9,ENTITY
*
         RACROUTE WORKA=RACWORK,                                       X
               REQUEST=AUTH,                                           X
               ENTITY=((R9)),                                          X
               ATTR=READ,          CHECK FOR READ ACCESS               X
               RELEASE=1.9,        USE RACF 1.9 EXPANSION              X
               MSGSUPP=YES,        SUPPRESS ICH408I MESSAGES           X
               LOG=ASIS,           LOG AS DEFINED IN RESOURCE CLASS    X
               MF=(E,RACMACRO)
         ST    R15,RETCODE
         C     R15,=F'8'
         BNH   ENDPROG
BADRC    DS    0H
         LA    R10,WORKAREA
         LR    R1,R15
         CALL  JCECVTBH
         MVC   ERRMSG(40),=CL40'SAF ERROR -    RC IS XXXXXXXX'
         STCM  R0,B'1111',ERRMSG+21
         STCM  R1,B'1111',ERRMSG+25
*        B     WTOMSG
ENDPROG  DS    0H
         L     R8,RETCODE
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
*
*
*
ENTITY   DC    CL39'TSSO.CMD.AUTH'
RACTEMP  RACROUTE REQUEST=AUTH,                                        X
               CLASS='FACILITY',                                       X
               RELEASE=1.9,                                            X
               MF=L
RACLEN   EQU   *-RACTEMP           LENGTH OF RACROUTE MACRO EXPANSION
*
*
*
*
***********************************************************************
* TSSORATH ONLY DATA AREA.                                            *
*                                                                     *
***********************************************************************
TSSORDAT DSECT
WORKAREA DS    CL512
CONSOLE  DS    F
RACUSER  DS    CL9          USERID FOR RACINIT.
RACPASS  DS    CL9          PASSWORD FOR RACINIT.
RETCODE  DS    F
ACEEADDR DS    F
ERRMSG   DS    CL40
*
RACWORK  DS    CL512
RACMACRO EQU    *
         ORG    *+RACLEN
*
*
         END   TSSORATH
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSORATH(''TSSO V 4.3.0  &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    TSSORATH'
 PUNCH ' NAME     TSSORATH(R)'
         END
