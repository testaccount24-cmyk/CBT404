TSSOSS10 CSECT
***********************************************************************
* THIS IS THE TSSO COMMAND INTERCEPT ROUTINE. CURRENT FUNCTION IS TO  *
* CHECK FOR A "#" AND PASS TO TSSO.                                   *
*                                                                     *
* NOTE: ALL MESSAGES START WITH TSSAXXXS WHERE 301 <= XXX <= 350      *
*       LAST MESSAGE NUMBER = 309                                     *
***********************************************************************
*                                                                     *
* MODIFIED:                                                           *
*   15OCT92  GLA  HANDLE HARDWARE SYSTEM CONSOLE UCMID (100) FOR      *
*                 ES/9000 MACHINES.                                   *
*                                                               EEJ1102
*   NOV 2002 EEJ  1) Support MCS extended consoles.             EEJ1102
*            EEJ  2) Remove restriction on non-TSSO commands    EEJ1102
*                    being issued by consoles that are not      EEJ1102
*                    logged on to TSSO. This function was       EEJ1102
*                    incompatible with the MCS LOGON function.  EEJ1102
*                                                                     *
***********************************************************************
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSO
         USING  TSSOSS10,R12       GET ADDRESSIBILITY
         B      ARINIT1
         DC     CL8'&SYSDATE'      GET DATE
         DC     CL8'&SYSTIME'      AND TIME FOR QVERS COMMAND
ARINIT1  DS     0H
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR     R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=1024
         LR     R11,R1
         USING  SS10DATD,R11
         LR     R10,R8
         USING  SSOB,R10
*
         GETCVT (R8)               FIND TSSO CVT                  RPS
         USING TSSOCVT,R8
*****************************************************************
* IF TSSO IS TERMINATED, UNDER NO CIRCUMSTANCES DO ANYTHING !
*****************************************************************
         LTR    R8,R8              ANY CVT ?
         BO    S34EXIT             NO, DONT DO A THING ...
*
         TM    TSSOFLG1,TSSOGONE   TERMINATING?
         BO    S34OEXIT            YES - IGNORE
*
*
*
         L     R9,SSOBINDV         POINT TO FUNCTION AREA
         USING SSCMBGN,R9          ADDRESS IT.
         L     R2,SSCMBUFF         POINT TO COMMAND BUFFER.
         LH    R5,0(,R2)           GET LENGTH
*                                                              MRM03077
***************************************************************MRM03077
* The passed length apparently includes the 4 byte header     *MRM03077
* mapped by the IEZMGCR macro, and the maximum length command *MRM03077
* is only 126 bytes.  We will subtract four from the passed   *MRM03077
* length, and use 126 as the maximum length command to check  *MRM03077
* for.  This was exposed because SDSF was passing a 126 byte  *MRM03077
* command buffer, regardless of the actual length of the      *MRM03077
* command entered.                                            *MRM03077
***************************************************************MRM03077
*                                                              MRM03077
         SH    R5,=H'4'
* COMMAND BUFFER FORMAT IS AS FOLLOWS:
* 2 BYTES: LENGTH
* 2 BYTES: WHO KNOWS
* 126 BYTES: COMMAND
         CH    R5,=H'126'          OUTRAGEOUS LENGTH?
         BH    S34OEXIT            YES - IGNORE IT
         LTR   R5,R5               NEGATIVE LENGTH ?
         BNP   S34OEXIT            YES - IGNORE
*                                                               EEJ1102
****************************************************************EEJ1102
*                                                              *EEJ1102
* At this point, when SECLEVEL=MAXIMAL, TSSO used to disallow  *EEJ1102
* commands issued from consoles that were not logged on to     *EEJ1102
* TSSO. Commands intended for TSSO were allowed in all cases.  *EEJ1102
*                                                              *EEJ1102
* This logic has been removed entirely. MCS consoles have had  *EEJ1102
* LOGON/LOGOFF capability for a long time so the function is   *EEJ1102
* no longer valuable. Also, this logic prevented the console   *EEJ1102
* from issuing the LOGON command itself. Thus, TSSO prevented  *EEJ1102
* MCS consoles from logging on to MCS. Some consoles *require* *EEJ1102
* LOGON/LOGOFF (e.g., SMCS consoles). Those consoles could not *EEJ1102
* be used with TSSO active in SECLEVEL=MAXIMAL mode.           *EEJ1102
*                                                              *EEJ1102
****************************************************************EEJ1102
*                                                               EEJ1102
***********************************************************************
* HERE, WE HAVE DETERMINED THAT THE OPERATOR IS SIGNED ON TO TSSO.    *
* WE WILL ALLOW (AT LEAST IN THIS RELEASE) ALL OPERATOR COMMANDS.     *
***********************************************************************
*
         TM    TSSOFLG2,TSSOMON    MONITORING?
         BNO   NM1
         WTO   'TSSA301A TSSO COMMAND INTERCEPT ROUTINE ACTIVE'
*****************************************************************
* TSSO SHOULD NOT INTERFERE WITH THIS OS COMMAND                *
*****************************************************************
NM1      DS    0H
         CLC   4(1,R2),TSSOCHAR    MINE?
         BNE   S34OEXIT            NO - IGNORE
CMDPROC  DS    0H
         TM    TSSOFLG2,TSSOMON    MONITORING?
         BNO   NM2
         WTO   'TSSA302A TSSO WILL PROCESS THIS COMMAND'
*****************************************************************
* HERE, WE HAVE DETERMINED THAT THE OS COMMAND SHOULD BE HANDLED*
* BY THE TSSO MAINLINE MODULE, IF TSSO IS AVAILABLE.            *
*****************************************************************
NM2      DS    0H
         TM    TSSOFLG1,TSSOGONE   TERMINATING?
         BNO   STAT2               YES - IGNORE
         WTO   'TSSA303A TSSO IS IN SHUTDOWN STATUS- CMD REJECTED'
         B     S34OEXIT            IGNORE COMMAND
STAT2    DS    0H
         L     R3,NEXTCMD          ADDRESS OF NEXT AVAILABLE
         USING TSSOCMND,R3         ADDRESS RING POSITION
*                                  COMMAND BUFFER
         TM    CMNDFLAG,X'80'      IS THIS BUFFER STILL BUSY.
         BNO   STAT3               YES - BRANCH
     WTO   'TSSA304A TSSO IS BUSY WITH PREVIOUS COMMNANDS'
     WTO   'TSSA304A (CONT) THIS MAY INDICATE A TSSO SYSTEM PROBLEM'
         B     S34BUSY
STAT3    DS    0H
         MVI   CMNDCMD,X'40'
         MVC   CMNDCMD+1(139),CMNDCMD  BLANK THE COMMAND AREA
         OI    CMNDFLAG,X'80'      TURN ON COMMAND FLAG
         BCTR  R5,0                LENGTH MINUS 1 FOR EX
         EX    R5,MOVECMD          COPY THE COMMAND
         B     AR1                 BRANCH AROUND MVC
MOVECMD  MVC   CMNDCMD(0),4(R2)    (EXECUTED)
AR1      DS    0H
         MVC   CMNDLENG(4),0(R2)   COPY THE LENGTH
*                                                               EEJ1102
         CLC   SSCMCNID,=F'0'      Is this a console ?          EEJ1102
         BNE   STAT4               Branch if yes                EEJ1102
*                                                               EEJ1102
* HERE, WE HAVE FOUND A COMMAND THAT WAS NOT ISSUED FROM A CONSOLE
* WE WILL INVALIDATE THE BUFFER BY RESETTING THE FLAG, AND
* ISSUE A MESSAGE TO THE CONSOLE, AND THE PROGRAMMER
         NI    CMNDFLAG,255-X'80'  INVALIDATE THE COMMAND
         B     S34EXITM            EXIT WITH A MESSAGE
STAT4    DS    0H
         MVC   CMNDCONS,SSCMCNID   Save 4-byte console ID       EEJ1102
         MVC   CMNDNAME,SSCMSCNM   Save 8-byte console name     EEJ1102
         MVC   CMNDAUTH,SSCMAUTH   Save console authorization   EEJ1102
         L     R2,CMNDNEXT
         ST    R2,NEXTCMD          DO THIS FOR NEXT TIME.
         TM    TSSOFLG2,TSSOOKFL   IS 'OK' TO BE SUPPRESSED?
         BO    S34NOACK            YES - BRANCH
         MVC   WORKAREA(OKMSGL),OKMSG Copy WTO parm list        EEJ1102
         WTO   CONSID=SSCMCNID,    Issue OK message             EEJ1102+
               MF=(E,WORKAREA)     (same)                       EEJ1102
         B     S34NOACK            Continue                     EEJ1102
*                                                               EEJ1102
OKMSG    WTO   '# OK',CONSID=,MF=L OK message parameter list    EEJ1102
OKMSGL   EQU   *-OKMSG             Length of OK message plist   EEJ1102
*                                                               EEJ1102
S34NOACK DS    0H
*****************************************************************
* NOW, THE IDEA IS TO POST THE TSSO MAINLINE ROUTINE, ONLY IF IT
* NEEDS TO BE POSTED. IF IT NEEDS TO BE POSTED, THEN THE WAIT
* BIT IN THE ECB SHOULD BE ON. WE WILL USE COMPARE AND SWAP
* FOR TWO REASONS. THE FIRST, IS THAT I NEVER USED A COMPARE AND
* SWAP, THE SECOND, IS THAT WE WANT TO ENSURE THAT TSSO DOESNT
* TURN THE WAIT BUT ON AFTER WE DO THE TM THAT WOULD BE REQUIRED, AND
* THEREFORE MISS THE POST.
*****************************************************************
         L     R2,$SVPOSTE         GET THE TSSO MAINLINE ECB
         L     R3,=X'40000000'
         TM    $SVPOSTE,X'80'      WAIT BIT ON ?
         BO    DOPOST              IF YES, DO THE POST.
         N     R2,=X'7FFFFFFF'    COMPARE OPERAND WITH WAIT BIT OFF
         CS    R2,R3,$SVPOSTE      ** COMPARE AND SWAP **
         BE    POSTDONE            COMPARE AND SWAP DID THE POST.
DOPOST   DS    0H
         LA    R5,$SVPOSTE          GET ECB TO POST
         L     R6,TSSOASCB          GET ADDRESS SPACE OF TSSO
         LA    R7,ENDPROG1          GET ERROR ROUTINE FOR XMEM POST
         LA    R1,$SVPOSTL          GET DSECT FOR XMEM POST
         SPACE
         POST  (R5),X'111',ASCB=(R6),ERRET=(R7),MF=(E,(1))
POSTDONE DS    0H
         B     S34MEXIT            INDICATE COMMAND IS MINE
*
S34BUSY  LA    R15,SSCMIMSG        COULD NOT EXECUTE
         B     S34EXIT             ISSUE IEE707I 'TEXT' NOT EXECUTED
*
S34MEXIT LA    R15,SSCMSUBC        MINE
         B     S34EXIT
*
S34OEXIT DS    0H
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
*
S34EXITM DS    0H                  NOT MINE, EXIT WITH MESSAGE
         WTO   'TSSA305E TSSO COMMANDS MAY NOT BE ISSUED FROM THIS ENVIX
               RONMENT',ROUTCDE=(2,11)
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
SS10LERR DS    0H
***********************************************************************
* WE COME HERE IF WE HAVE A LOGICAL ERROR IN TSSOSS10. WE WILL ISSUE  *
* A MESSAGE, DENY EVERYTHING, AND TERMINATE.                          *
***********************************************************************
         XR    R15,R15
         WTO   'TSSA307S SEVERE ERROR IN TSSOSS10 - CONTACT TSSO SYSTEMX
               S PROGRAMMER'
S34EXIT  DS    0H
         ST    R15,SSOBRETN
         FREEMAIN R,LV=1024,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         XR    R15,R15
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
***********************************************************************
* TSSOSS10 CONSTANTS AREA.                                            *
*                                                                     *
***********************************************************************
***********************************************************************
* TSSOSS10 DATA AREA.                                                 *
*                                                                     *
***********************************************************************
SS10DATD DSECT
WORKAREA DS    CL200
CONSID   DS    F                 CONSOLE ID ISSUING THIS COMMAND
SAVER10  DS    F                 REGISTER 10 WHEN USING JCE ROUTINE
MVSCMD   DS    CL100
         CVT   DSECT=YES
         IEFJESCT
         IEFJSSVT
         IEFJSCVT
         IEFJSSOB (CM,WT),CONTIG=NO
UCM      DSECT
         IEECUCM
         TSSOCVT
         TSSOCMND
         TSSOSECR
         IHAWQE
         IHAORE
         EQUATES
         END   TSSOSS10
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOSS10(''TSSO VERSION 4.2 &SYSDATE &SYSTIME'')'
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    TSSOSS10'
 PUNCH ' NAME     TSSOSS10(R)'
         END
